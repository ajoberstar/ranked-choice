plugins {
  id 'gradle-clojure.clojure' version '0.4.0-beta.6'
  id 'gradle-clojure.clojurescript-base' version '0.4.0-beta.6'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '2.0.4'
  id 'org.ajoberstar.grgit' version '2.2.0'
  id 'org.ajoberstar.reckon' version '0.6.0'
}

mainClassName = 'ranked_choice.main'

reckon {
  normal = scopeFromProp()
  preRelease = snapshotFromProp()
}

repositories {
  mavenCentral()
  maven {
    name = 'clojars'
    url = 'https://repo.clojars.org/'
  }
}

dependencies {
  // backend
  implementation 'org.clojure:clojure:latest.release'
  implementation 'org.clojure:tools.logging:latest.release'
  implementation 'mount:mount:latest.release'
  implementation 'org.clojure:core.async:latest.release'

  // http
  implementation 'ring:ring-core:latest.release'
  implementation 'ring:ring-jetty-adapter:latest.release'
  implementation 'org.eclipse.jetty:jetty-server:latest.release'

  // routing
  implementation 'bidi:bidi:latest.release'

  // rendering
  implementation 'hiccup:hiccup:latest.release'

  // frontend
  implementation 'org.clojure:clojurescript:latest.release'
  implementation 're-frame:re-frame:latest.release'

  // testing
  testImplementation 'junit:junit:latest.release'

  // development
  devImplementation 'com.bhauman:figwheel-main:latest.integration'

  // TODO remove these old deps
  implementation 'http-kit:http-kit:latest.release'
  implementation 'com.stuartsierra:component:latest.release'
  devImplementation 'com.stuartsierra:component.repl:latest.release'
  implementation 'compojure:compojure:latest.release'
  implementation 'enlive:enlive:latest.release'

  constraints {
    devImplementation('org.clojure:java.classpath:0.3.0') {
      because 'Previous versions are not compatible with Java 9'
    }
  }
}

dependencyLocking {
  lockAllConfigurations()
}

compileClojure {
  options {
    aotCompile = true
  }
}

compileClojurescript {
  options {
    outputTo = 'public/js/main.js'
    outputDir = 'public/js'
    assetPath = 'js'
    main = 'org.ajoberstar.dev'
    optimizations = 'none'
  }
}

compileDevClojurescript {
  options {
    outputDir = 'public/js'
  }
}
