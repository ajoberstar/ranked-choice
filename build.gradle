plugins {
  id 'gradle-clojure.clojure' version '0.4.0-beta.10'
  id 'gradle-clojure.clojurescript' version '0.4.0-beta.10'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '2.0.4'
  id 'org.ajoberstar.grgit' version '2.2.1'
  id 'org.ajoberstar.reckon' version '0.7.0'
}

mainClassName = 'ranked_choice.main'

reckon {
  scopeFromProp()
  snapshotFromProp()
}

repositories {
  mavenCentral()
  maven {
    name = 'clojars'
    url = 'https://repo.clojars.org/'
  }
}

dependencies {
  // backend
  implementation 'org.clojure:clojure:latest.release'
  implementation 'org.clojure:tools.logging:latest.release'
  implementation 'mount:mount:latest.release'
  implementation 'org.clojure:core.async:latest.release'

  // http
  implementation 'ring:ring-core:latest.release'
  implementation 'ring:ring-defaults:latest.release'
  implementation 'ring:ring-json:latest.release'
  implementation 'http-kit:http-kit:latest.release'

  // routing
  implementation 'bidi:bidi:latest.release'

  // rendering
  implementation 'hiccup:hiccup:latest.release'

  // frontend
  implementation 'org.clojure:clojurescript:latest.release'
  implementation 're-frame:re-frame:latest.release'
  implementation 're-posh:re-posh:latest.release'

  // testing
  testImplementation 'junit:junit:latest.release'

  // development
  devImplementation 'binaryage:devtools:latest.release'
  devImplementation 'day8.re-frame:re-frame-10x:latest.release'
  devImplementation 'devcards:devcards:latest.release'
}

dependencyLocking {
  lockAllConfigurations()
}

clojurescript {
  builds {
    all {
      compiler {
        outputDir = 'public/js/out'
        assetPath = 'js/out'
        modules {
          main {
            outputTo = 'public/js/main.js'
            entries = ['ranked-choice.client']
          }
          cards {
            outputTo = 'public/js/cards.js'
            entries = ['ranked-choice.cards']
          }
        }
      }
    }
    main {
      compiler {
        optimizations = 'advanced'
      }
    }
    dev {
      compiler {
        optimizations = 'none'
        preloads = ['ranked-choice.dev', 'devtools.preload', 'day8.re-frame-10x.preload']
      }
    }
  }
}
